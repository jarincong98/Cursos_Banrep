%% Modelo RBC / Economía cerrada
%% Autor: Óscar Ávila

%----------------------------------------------------------------
% Paso 1: Definición de variables endógenas
%----------------------------------------------------------------
var 
    W       $W$     (long_name = 'Salario') 
    R_K     $R^{K}$ (long_name = 'Renta del capital') 
    MC      $MC$    (long_name = 'Costos marginales') 
    L       $L$     (long_name = 'Labor') 
    K       $K$     (long_name = 'Capital') 
    Y       $Y$     (long_name = 'Production') 
    I       $I$     (long_name = 'Investment') 
    C       $C$     (long_name = 'Consumption') 
    A       $A$     (long_name = 'Productivity') 
;

%----------------------------------------------------------------
% Paso 2: Definición de variables exógenas
%----------------------------------------------------------------
varexo 
    eps_A   $\epsilon^{A}$ (long_name = 'Productivity shock') 
;

%----------------------------------------------------------------
% Paso 3: Parámetros del modelo
%----------------------------------------------------------------
parameters  
    sigma   $\sigma$    (long_name = 'Inverse of intertemporal subs elasticity')
    bbeta   $\beta$     (long_name = 'Discount factor')
    delta   $\delta$    (long_name = 'Capital depreciation') 
    aalpha  $\alpha$    (long_name = 'Capital share') 
    psi_l   $\phi^{L}$  (long_name = 'psi L') 
    eta     $\eta$      (long_name = 'Frish elasticity') 
    rho     $\rho_{A}$  (long_name = 'Productivity persisitence') 
    Ass     $A$         (long_name = 'Productivity steady state') 
;


%----------------------------------------------------------------
% set parameter values 
%----------------------------------------------------------------

% load params.mat;
% load SSvar.mat;
% 
sigma  = 2;     %params(1);
bbeta  = 0.98;  %params(2);
delta  = 0.05;  %params(3);
aalpha = 0.3;   %params(4);
psi_l  = 1;     %params(5);
eta    = 1.5; %params(6);
rho    = 0.75;%params(7);
Ass    = 1;%params(8);

%----------------------------------------------------------------
% Paso 4. Bloque del modelo
%----------------------------------------------------------------
model;
[name = 'Función de producción']
    Y = A*K(-1)^aalpha*L^(1-aalpha);

[name = 'Demanda de capital']
    R_K = aalpha*MC*(Y/K(-1));

[name = 'Ley de acumulación de capital']
    K = (1-delta)*K(-1) + I;

[name = 'Demanda de trabajo']
    W = (1-aalpha)*MC*(Y/L);

[name = 'Oferta de trabajo']
    psi_l*L^eta*C^sigma = W;

[name = 'Costos marginales']
    MC = (1/A)*(R_K/aalpha)^aalpha*(W/(1-aalpha))^(1-aalpha);

[name = 'Ecuación de Euler']
    C^(-sigma) = bbeta*C(+1)^(-sigma)*(1 - delta + R_K);

[name = 'Productividad']
    A = A(-1)^rho*Ass^(1-rho)*(1+eps_A);

[name = 'Demanda agregada']
     Y = C + I;
end;

% Escritura del modelo en LaTeX
write_latex_parameter_table;
write_latex_definitions;
write_latex_original_model(write_equation_tags);
collect_latex_files;

% return;
%----------------------------------------------------------------
% Paso 5. Opciones de Dynare
%----------------------------------------------------------------
resid;              % Cálculo de residuales
check;              % Chequeo de condiciones de Blanchar y Kahn
steady;             % Cálculo del estado estacionario
model_diagnostics;  % Diagnóstico del modelo

%----------------------------------------------------------------
% Paso 6. Productos de Dynare
%----------------------------------------------------------------
shocks;
    var eps_A = 0.01;
end;
% Simulación estocástica
    stoch_simul(periods = 1000, irf=40) Y C I L K;

